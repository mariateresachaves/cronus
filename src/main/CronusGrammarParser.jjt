/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
  	TRACK_TOKENS = true;
  	MULTI = true;
	STATIC = true;
	NODE_EXTENDS = "MyNode";
	LOOKAHEAD = 1;
}

PARSER_BEGIN(CronusGrammarParser)
package main;

public class CronusGrammarParser
{
	/**
	* Returns the root node of the AST.
	* It only makes sense to call this after a successful parse.
	* @return the root node
	*/
	public Node rootNode() {
    	return jjtree.rootNode();
 	}
 	
}

PARSER_END(CronusGrammarParser)

SKIP :
{
  	" "
|  "\n"
| 	"\r"
| 	"\r\n"
}

TOKEN : /* --- LITERALS --- */
{
	< #DIGIT   : ["0"-"9"] >
| 	< #LOWER   : ["a"-"z"] >
| 	< #UPPER   : ["A"-"Z"] >
}

TOKEN : /* --- RESERVED WORDS --- */
{
  	< NEW           : "new">
| 	< BOOL          : "true" | "false" >
|   < LABEL         : "label" >
|   < DEGREE        : "degree" >
|   < WEIGHT        : "weight" >
|   < SHORTESTPATH  : "shortestPath" >
|   < DEPTHSEARCH   : "depthSearch" >
|   < BREADTHSEARCH : "breadthSearch" >
|   < IN            : "in" >
|   < OUT           : "out" >
|   < EDGES         : "edges" >
|   < EDGEF         : "edge" >
|   < NODES         : "nodes" >
|   < NODEF         : "node" >
}

TOKEN : /* --- CLASSES --- */
{
  	< GRAPH    : "Graph" >
| 	< NODE     : "Node" >
| 	< EDGE     : "Edge" >
| 	< VARIABLE : ("_" | < LOWER > | < UPPER > )+ ("_" | < LOWER > | < UPPER > | < DIGIT > )* >
|   < STRING   : "\"" (~["\"", "\\"] | "\\\"" | "\\\\")* "\"" >
| 	< INTEGER  : ("+" | "-")? (< DIGIT >)+ >
| 	< FLOAT    : ("+" | "-")? ((< DIGIT >)*".")?(< DIGIT >)+ >
}

TOKEN : /*--- OPERATORS --- */
{
   	< OPAR    : "(" > 
| 	< CPAR    : ")" >
| 	< OBR     : "{" >
| 	< CBR     : "}" >
| 	< OSQBR   : "[" >
| 	< CSQBR   : "]" >
| 	< COL     : ":" >
| 	< SCOL    : ";" >
| 	< COMMA   : "," >
| 	< EQ      : "=" >
| 	< DOT     : "." >
| 	< QUOTE   : "\"" | "'" >
| 	< COMP    : ">" | "<" | "<=" | ">=" | "==" >
}

TOKEN : /* --- FUNCTIONS --- */
{
  	< PATH : ("\"" | "'") ( ( (< LOWER > | < UPPER >) ":")? ("\\"(< LOWER > | < UPPER > | < DIGIT > | "_" | "." | "-")+ )+("\\")?
    	   | ("/")?((< LOWER > | < UPPER > | < DIGIT > | "_" | "." | "-")+"/")+("/")? )
        	("\"" | "'") >
}

/*
	== START FUNCTION ==
*/
SimpleNode Start() :
{
}
{
 	(
 	    LOOKAHEAD(3)
    	Declaration() 
	|
		LOOKAHEAD(2)
    	NewGraph()
	|
		LOOKAHEAD(2)
   		ListNodes()
	|
   		SearchGraph()
  	)+
  	< EOF >
  	{return jjtThis;}
}

/*
	== DECLARATION OF VARIABLES ==
*/
void Declaration() :
{
  	Token g, e, n, v, scol;
}
{
  	(
  	  	g = < GRAPH > {jjtThis.val = g.image;} 			#Graph
	|
	  	e = < EDGE > {jjtThis.val = e.image;} 			#Edge
	|
		LOOKAHEAD(2)	  	n = < NODE > {jjtThis.val = n.image;} 			#Node	)
	v    = < VARIABLE > {jjtThis.val = v.image;} 		#Variable
  	scol = < SCOL > {jjtThis.val = scol.image;} 		#Semicolon
}

/*
	== NEW GRAPH ==
*/
void NewGraph() :
{
  	Token g, e, n, osqbr, csqbr, v, eq, scol;
}
{
  	(
		(
	  	  	g = < GRAPH > {jjtThis.val = g.image;} 			#Graph
		)?
		v = < VARIABLE > {jjtThis.val = v.image;} 			#Variable
  	)
	eq   = < EQ > {jjtThis.val = eq.image;} 				#Equal
	NewGraphRight()
  	scol = < SCOL > {jjtThis.val = scol.image;} 			#Semicolon
}

/*
	== RIGHT HAND SIDE OF A NEW GRAPH ==
*/
void NewGraphRight() #NewGraphRight :
{
  	Token n, g, p, opar, cpar;
}
{
  	n    = < NEW > {jjtThis.val = n.image;} 	#New
  	g    = < GRAPH > {jjtThis.val = g.image;}	#Graph
  	opar = < OPAR > {jjtThis.val = opar.image;} #OpenParenthesis
  	p    = < PATH > {jjtThis.val = p.image;} 	#Path
  	cpar = < CPAR > {jjtThis.val = cpar.image;} #CloseParenthesis
}

/*
	== LIST ALL NODES ACCORDING TWO PARAMETERS (IF ANY) ==
*/
void ListNodes() :
{
  	Token eq, scol, v;
}
{
  	NodeListDec()
  	(
    	eq = < EQ > {jjtThis.val = eq.image;} 	#Equal
    	ListNodesRight()
  	)?
  	scol = < SCOL > {jjtThis.val = scol.image;} #Semicolon
|
	v = < VARIABLE > {jjtThis.val = v.image;} 	#Variable
  	eq = < EQ > {jjtThis.val = eq.image;} 		#Equal
  	ListNodesRight()
  	scol = < SCOL > {jjtThis.val = scol.image;} #Semicolon
}

/*
	== NODE LIST DECLARATION ==
*/
void NodeListDec() :
{
  	Token n, obr, cbr, v;
}
{
  	n = < NODE > {jjtThis.val = n.image;} 		#Node
  	obr = < OSQBR > {jjtThis.val = obr.image;} 	#OpenSquareBracket
  	cbr = < CSQBR > {jjtThis.val = cbr.image;} 	#CloseSquareBracket
  	v = < VARIABLE > {jjtThis.val = v.image;} 	#Variable
}

/*
	== RIGHT SIDE OF DECLARATION TO GET LIST OF NODES ==
*/
void ListNodesRight() :
{
	Token v, d, n, o, c;
}
{
  	v = < VARIABLE > {jjtThis.val = v.image;} 	#Variable
  	d = < DOT > {jjtThis.val = d.image;}		#Dot
  	n = < NODES > {jjtThis.val = n.image;}		#Nodes
  	o = < OPAR > {jjtThis.val = o.image;}		#OpenParenthesis
  	AllParametersNode()
  	c = < CPAR > {jjtThis.val = c.image;}		#CloseParenthesis
}

/*
	== FILTER SEARCH NODE WITH ALL THREE PARAMETERS ==
*/
void AllParametersNode() :
{
  	Token comma;
}
{
	(
	  	LOOKAHEAD(2)		NodeLabel()
	  	(
	    	comma = < COMMA > {jjtThis.val = comma.image;} 		#Comma
	    	NodeDegree()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;} 	#Comma
		      NodeProperties()
		    )?
	    )?
	|
		NodeLabel()
	  	(
		    
		    comma = < COMMA > {jjtThis.val = comma.image;} 		#Comma
	    	NodeProperties()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
		      NodeDegree()
		    )?
	    )?
	|
		LOOKAHEAD(2)
		NodeDegree()
	  	(
	    	comma = < COMMA > {jjtThis.val = comma.image;}		#Comma
	    	NodeLabel()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
		      NodeProperties()
		    )?
	    )?
	|
		NodeDegree()
	  	(
	    	comma = < COMMA > {jjtThis.val = comma.image;}		#Comma
	    	NodeProperties()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
		      NodeLabel()
		    )?
	    )?
	|
		LOOKAHEAD(2)
		NodeProperties()
	  	(
	    	comma = < COMMA > {jjtThis.val = comma.image;}		#Comma
	    	NodeLabel()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
		      NodeDegree()
		    )?
	    )?
	|
		NodeProperties()
	  	(
			comma = < COMMA > {jjtThis.val = comma.image;}		#Comma
	    	NodeDegree()
		    (
		      comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
		      NodeLabel()
		    )?
	    )?
	)?
}

/*
	== FILTER NODE PARAMETER LABEL ==
*/
void NodeLabel() :
{
  	Token label, eq;
}
{
  	label = < LABEL > {jjtThis.val = label.image;}	#Label
 	eq = < EQ > {jjtThis.val = eq.image;}			#Equal
 	Value()
}

/*
	== FILTER NODE PARAMETER DEGREE ==
*/
void NodeDegree() :
{
  	Token degree;
}
{
  	degree = < DEGREE > {jjtThis.val = degree.image;}	#Degree
  	CompareValues()
}

/*
	== TYPES OF VALUES: STRING OR NUMBER ==
*/
void Value() :
{
  	Token s, i, f;
}
{
  	s = < STRING > {jjtThis.val = s.image;}		#String
|   i = < INTEGER > {jjtThis.val = i.image;}	#Integer
|   f = < FLOAT > {jjtThis.val = f.image;}		#Float
}

/*
	== SEARCH ==
*/
void SearchGraph() :
{
	Token v, d, opar, v1, comma, v2, cpar, scol;
}
{
 	v = < VARIABLE > {jjtThis.val = v.image;}				#Variable
 	d = < DOT > {jjtThis.val = d.image;}					#Dot
 	SearchType()
  	opar = < OPAR > {jjtThis.val = opar.image;}				#OpenParenthesis
  	v1 = < VARIABLE > {jjtThis.val = v1.image;}				#Variable
  	comma = < COMMA > {jjtThis.val = comma.image;}			#Comma
  	v2 = < VARIABLE > {jjtThis.val = v2.image;}				#Variable
	MoreOptions()
   	cpar = < CPAR > {jjtThis.val = cpar.image;}				#CloseParenthesis
 	scol = < SCOL > {jjtThis.val = scol.image;}				#Semicolon
}

/*
	== SEARCH TYPE ==
*/
void SearchType() :
{
  	Token sp;
}
{
  	sp = < SHORTESTPATH > {jjtThis.val = sp.image;}		#ShortestPath
|
 	sp = < DEPTHSEARCH > {jjtThis.val = sp.image;}		#DepthSearch
|
	sp = < BREADTHSEARCH > {jjtThis.val = sp.image;}	#BreadthSearch
}

/*
	== MORE OPTIONS ==
*/
void MoreOptions() :
{
  	Token comma;
}
{
  	(
	  	comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
	  	NodeProperties()
	)?
}

/*
 	== FILTER NODE PARAMETER PROPERTIES ==
*/
void NodeProperties() :
{
  	Token comma, osqbr, csqbr, key;
}
{
  	osqbr = < OSQBR > {jjtThis.val = osqbr.image;}		#OpenSquareBracket
  	key = < VARIABLE > {jjtThis.val = key.image;}		#Variable
  	CompareValues()
  	(
    	comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
    	key = < VARIABLE > {jjtThis.val = key.image;}	#Variable
    	CompareValues()
  	)*
  	csqbr = < CSQBR > {jjtThis.val = csqbr.image;}		#CloseSquareBracket
}

/*
	== DEFINE WAYS TO COMPARE VARIABLE WITH VALUES ==
*/
void CompareValues() :
{
  	Token eq, os, s, i, f, cs, comma, comp;
}
{
  	// Equals to an Interval
  	eq = < EQ > {jjtThis.val = eq.image;}			#Equal
  	IntervalBracket()
  	DataToCompare()
  	comma = < COMMA > {jjtThis.val = comma.image;}	#Comma
  	Value()
  	IntervalBracket()
|
	// Compares to a Value
	comp = < COMP > {jjtThis.val = comp.image;}		#Compares
	DataToCompare()
}

/*
	== INTERVAL BRACKET ==
*/
void IntervalBracket() :
{
  	Token os, cs;}{
  	os = < OSQBR > {jjtThis.val = os.image;}	#OpenSquareBracket
|
	cs = < CSQBR > {jjtThis.val = cs.image;}	#CloseSquareBracket}

/*
	== DATA TYPE TO COMPARE ==
*/
void DataToCompare() :
{
  	Token s, i, f;}{
  	s = < STRING > {jjtThis.val = s.image;}		#String
|
  	i = < INTEGER > {jjtThis.val = i.image;}	#Integer
|
  	f = < FLOAT > {jjtThis.val = f.image;}		#Float}